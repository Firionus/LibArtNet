plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'de.deltaeight'
version '1.1.2-beta'

archivesBaseName = 'LibArtNet'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {

    implementation 'com.google.code.gson:gson:2.8.6'

    // Test framework
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat 'full'
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            from components.java

            pom {
                name = 'LibArtNet'
                description = 'Art-Net 4 protocol implementation in Java'
                url = 'https://github.com/deltaeight/LibArtNet'

                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'schw4rzlicht'
                        name = 'Julian Rabe'
                        email = 'julian@deltaeight.de'
                    }
                }

                scm {
                    connection = 'scm:https://github.com/deltaeight/LibArtNet.git'
                    developerConnection = 'scm:https://github.com/deltaeight/LibArtNet.git'
                    url = 'https://github.com/deltaeight/LibArtNet'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}